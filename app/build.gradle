apply plugin: 'com.android.application'

android {

    signingConfigs {
        config {
            storeFile file('C:/Users/sergi/AndroidStudioProjects/keystore.jks')
        }
    }

    buildToolsVersion "28.0.0"
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.sergiocruz.capstone"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    // use Java 8 Lambdas
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Use keys in gradle.properties. Add to .gitignore
    // get key in Java: BuildConfig.SOME_API_KEY
    // get key in Java: BuildConfig.ANOTHER_API_KEY
    buildTypes.each {
        it.buildConfigField 'String', 'TWITTER_API_KEY', TwitterApiKey
        it.buildConfigField 'String', 'TWITTER_API_SECRET', TwitterApiSecret
    }

}

ext {
    supportlibversion = "27.1.1"
    constraintLayoutVersion = "1.1.2"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:support-v4:$supportlibversion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "com.android.support:appcompat-v7:$supportlibversion"
    implementation "com.android.support:design:$supportlibversion"
    implementation "com.android.support:recyclerview-v7:$supportlibversion"
    implementation "com.android.support:cardview-v7:$supportlibversion"

    // ConstraintLayout
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.android.support.constraint:constraint-layout-solver:$constraintLayoutVersion"

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'

    // Facebook login
    implementation 'com.facebook.android:facebook-android-sdk:4.33.0'

    // Twitter login
    implementation 'com.twitter.sdk.android:twitter-core:3.1.1'

    // Glide Image Loading
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    // Room
    implementation "android.arch.persistence.room:runtime:1.1.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.0"

    // MVVM
    implementation "android.arch.lifecycle:extensions:1.1.1"
    // Java8 support for Lifecycles instead of annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    implementation "android.arch.lifecycle:common-java8:1.1.1"

    // Paging
    implementation "android.arch.paging:runtime:1.0.0"

    // Firebase JobDispatcher
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

}

apply plugin: 'com.google.gms.google-services'
